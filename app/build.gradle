plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 34
    defaultConfig {
        applicationId "com.neew.browser"
        minSdkVersion 21
        targetSdkVersion 34
        // versionCode and versionName will be set below using Git
        // ndk {
        //     abiFilters "arm64-v8a"
        // }
        versionCode 171
        versionName "1.6.11"
    }
    signingConfigs {
        release {
            storeFile file("indic-browser-key.jks")
            storePassword "IndicBrowser2025"
            keyAlias "indic-browser-key"
            keyPassword "IndicBrowser2025"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    namespace 'com.neew.browser'

    def getVersionNameFromGit = { ->
        try {
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'describe', '--tags', '--abbrev=0'
                standardOutput = stdout
            }
            return stdout.toString().trim()
        } catch (Exception e) {
            return "1.0.0"
        }
    }
    def getVersionCodeFromGit = { ->
        try {
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'rev-list', '--count', 'HEAD'
                standardOutput = stdout
            }
            return Integer.parseInt(stdout.toString().trim())
        } catch (Exception e) {
            return 1
        }
    }
    defaultConfig.versionName = getVersionNameFromGit()
    defaultConfig.versionCode = getVersionCodeFromGit()

    splits {
        abi {
            enable true
            reset()
            include 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
            universalApk false // Set to true if you also want a universal APK
        }
    }
}

ext {
    geckoviewVersion = "129.0.20240801122119"
    geckoviewChannel = "stable"
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "org.mozilla.geckoview:geckoview:129.0.20240801122119"
    implementation 'androidx.activity:activity:1.8.0'
    implementation 'com.google.firebase:firebase-analytics:21.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
} 

apply plugin: 'com.google.gms.google-services' 